apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.sap.odata.android'

buildscript {

    repositories rootProject.ext.repos

    dependencies {
        classpath group: 'com.sap.cloud.android', name:'odata-android-gradle-plugin', version:sdkVersion
    }
}

odata {
   verbose true
    services {
      // connection id: com.sap.mobile.mahlwerk
      odataservice{
        schemaFile file("src/main/res/raw/com_sap_mobile_mahlwerk.xml")
        packageName "com.sap.cloud.android.odata.odataservice"
        serviceClass "OdataService"
      }
   }
}



android {
    compileSdkVersion 29
    defaultConfig {
        multiDexEnabled true
        applicationId "com.sap.mobile.mahlwerk"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        dataBinding.enabled = true
        ndk {
            // Exclude 64 bit libraries.  Offline OData currently doesn't have 64bit support.
            abiFilters "armeabi-v7a", "x86"
        }
    }

    dataBinding {
        enabled = true
    }

    androidExtensions {
        experimental = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
}

configurations {
    all {
        exclude module: 'httpclient'
        exclude module: 'commons-logging'
        exclude module: 'opengl-api'
        exclude module: 'xmlParserAPIs'
        exclude module: 'xpp3'
        exclude module: 'android'
    }
}

dependencies {
    // Android framework dependencies
    implementation group: 'androidx.appcompat', name: 'appcompat', version: '1.1.0'
    implementation group: 'androidx.legacy', name: 'legacy-support-v4', version: '1.0.0'
    implementation group: 'androidx.recyclerview', name: 'recyclerview', version: '1.0.0'
    implementation group: 'com.google.android.material', name: 'material', version: '1.0.0'
    implementation group: 'androidx.preference', name: 'preference', version: '1.1.0'
    implementation group: 'androidx.constraintlayout', name: 'constraintlayout', version: '1.1.3'
    implementation group: 'androidx.navigation', name: 'navigation-fragment-ktx', version: '2.1.0'
    implementation group: 'androidx.navigation', name: 'navigation-ui-ktx', version: '2.1.0'
    implementation group: 'com.google.android.gms', name: 'play-services-maps', version: '17.0.0'

    // Android Architecture Components
    implementation group: 'androidx.lifecycle', name: 'lifecycle-common-java8', version: '2.1.0'
    implementation group: 'androidx.lifecycle', name: 'lifecycle-extensions', version: '2.1.0'

    // JUnit dependency
    testImplementation 'junit:junit:4.12'

    // SAP Cloud Android SDK dependencies
    implementation group: 'com.sap.cloud.android', name: 'fiori', version: sdkVersion
    implementation group: 'com.sap.cloud.android', name: 'onboarding', version: sdkVersion
    implementation group: 'com.sap.cloud.android', name: 'odata', version: sdkVersion
    implementation group: 'com.sap.cloud.android', name: 'foundation', version: sdkVersion
    implementation group: 'com.sap.cloud.android', name: 'offline-odata', version: sdkVersion
    implementation group: 'com.sap.cloud.android', name: 'google-maps', version: sdkVersion


    // Logging dependency
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.github.tony19:logback-android:2.0.0'


    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'

    // For UI testing
    androidTestImplementation 'com.pgs-soft:espressodoppio:1.0.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestUtil 'androidx.test:orchestrator:1.2.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'com.google.android.gms:play-services-location:17.0.0'
}


